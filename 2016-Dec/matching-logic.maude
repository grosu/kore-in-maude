mod KSTRING is
  sort String .
  op <Strings> : -> String [special(id-hook StringSymbol())] .
endm

mod MATCHING-LOGIC is
*** Strings are basic patterns, but they must only be used to encode model
*** values, i.e., int("123"), bool("true"), etc.  In general, sort("string")
  including KSTRING .
*** Basic term-like syntax for structure:
  sorts Symbol Variable Pattern PatternList .
  subsorts String Variable < Pattern < PatternList .
  op _`(_`) : Symbol PatternList -> Pattern
    [prec 0 strat(2 0)] .
  --- We used ` above for escaping parentheses.
  --- The attribure strat(2 0) says that the first argument should never be
  --- evaluated; this is important because we want to allow constant symbols
  --- in the E-MATCHING-LOGIC module below, and then desugar them.
  op \. : -> PatternList .
  op _,_ : PatternList PatternList -> PatternList
    [prec 100 assoc id: \.] .
  --- Note that we used `assoc` although this is a syntax for parsing;
  --- if too slow, then use the `gather` attribute.
*** Logical constructs:
  op \true : -> Pattern .
  op \false : -> Pattern .
  op \and : Pattern Pattern -> Pattern .
  op \or : Pattern Pattern -> Pattern .
  op \not : Pattern -> Pattern .
  op \implies : Pattern Pattern -> Pattern .
  op \exists : Variable Pattern -> Pattern .
  op \forall : Variable Pattern -> Pattern .
  op \next : Pattern -> Pattern .
  op \rewrite : Pattern Pattern -> Pattern .
  op \equal : Pattern Pattern -> Pattern .
endm

mod E-MATCHING-LOGIC is
*** Extending ML with sugar syntax allowing constant symbols to not take
*** the empty list of arguments.
*** In the future, we may also move the derived ML constructs above here,
*** as well as other derived constructs that we may like or need.
  including MATCHING-LOGIC .
  subsort Symbol < Pattern .
  var S : Symbol .  var P P1 P2 : Pattern .  var V : Variable .
  eq S = S(\.) .
*** Sugared notation for ML pattern constructs
  op _\and_ : Pattern Pattern -> Pattern [assoc prec 55] .
  eq P1 \and P2 = \and(P1,P2) .
  op _\or_ : Pattern Pattern -> Pattern [assoc prec 59] .
  eq P1 \or P2 = \or(P1,P2) .
---  op \not_ : Pattern -> Pattern [prec 53] .
  op _\implies_ : Pattern Pattern -> Pattern [prec 61 gather(e E)] .
  eq P1 \implies P2 = \implies(P1,P2) .
  op \exists_._ : Variable Pattern -> Pattern .
  eq \exists V . P = \exists(V,P) .
  op \forall_._ : Variable Pattern -> Pattern .
  eq \forall V . P = \forall(V,P) .
---  op \next_ : Pattern -> Pattern .
  op _=>_ : Pattern Pattern -> Pattern [prec 60] .
  eq P1 => P2 = \rewrite(P1,P2) .
  op _=_ : Pattern Pattern -> Pattern [prec 60] .
  eq (P1 = P2) = \equal(P1,P2) .
endm

